#include <DHT.h> //Biblioteca sensor temperatura
#include <DHT_U.h> //Biblioteca sensor umidade

#include <Wire.h> //Biblioteca utilizada gerenciar a comunicação entre dispositicos através do protocolo I2C
#include <LiquidCrystal_I2C.h> //Biblioteca controlar display 16x2 através do I2C

#include <avr/io.h> //Biblioteca interruptores (dimmer)
#include <avr/interrupt.h> //Biblioteca interruptores (dimmer)

#include <PID_v1.h> //Biblioteca PID arduino

#define PINO_ZC 2    // passagem por 0
#define PINO_DIM 3   // pwm

#define PULSO 4    // tempo em que o pino DIM ficara em HIGH, contado em ciclos do Timer1
#define FRACO 60   // intensidade minima desejada para a lampada (0 a ~520) 
#define FORTE 485  // intensidade maxima desejada para a lampada (0 a ~520) 

#define col  16 //Define o número de colunas do display utilizado
#define lin   2 //Define o número de linhas do display utilizado
#define ende  0x27 //Define o endereço do display
#define DHTPIN A1 // Pino sensor temperatura/umidade
#define DHTTYPE DHT11 // DHT 11
#define pinopotenc A2 // Pino Potenciometro

DHT dht(DHTPIN, DHTTYPE);

int buttonPin = 8;//Define buttonPin no pino digital 8

bool ligado=false; //variável para detectar se o push button foi pressionado

LiquidCrystal_I2C lcd(ende, col, lin); //Cria o objeto lcd passando como parâmetros o endereço, o nº de colunas e o nº de linhas

unsigned long lastSend=0; //detectart o último envio da intensidade da lâmpada

double Setpoint, Input, Output; // entradas e saidas PID

double kp=1.5, ki=2 , kd=1; // variáveis PID

PID myPID(&Input, &Output, &Setpoint,kp,ki,kd,DIRECT);
 
void setup() 
{
  dht.begin(); //Inicializa o sensor de umidade e temperatura
  pinMode(pinopotenc, INPUT);
  pinMode(buttonPin , INPUT);
  lcd.init(); //Inicializa a comunicação com o display já conectado
  lcd.clear(); //Limpa a tela do display
  lcd.backlight(); //Aciona a luz de fundo do display

  pinMode(PINO_ZC, INPUT_PULLUP);
  pinMode(PINO_DIM, OUTPUT);

  Serial.begin(115200); // Inicia Serial com uma velocidade alta para acompanhar bem o Serial plotter 
  Input= int(dht.readTemperature()); //Leitura temperatura
  Setpoint=int((analogRead(pinopotenc)/34.1)+25); //Leitura do potenciometro com a escala convertida  
  myPID.SetMode(AUTOMATIC);// Inicia o PID

  // ativa o Timer/Counter1 com os modos dos bits 0 e 1, ou seja, ativa Comparador A e detector de overflow
  TIMSK1 = 0x03;    // 16.11.8 na datasheet 

  // desabilita inicialmente o timer:
  TCCR1A = 0x00;    // 16.11.1 na datasheet   
  TCCR1B = 0x00;    // 16.11.2 na datasheet
  
}

void defineIntensidade(int k) { 
  // recebe um valor de 0 a 1024 para a intensidade da lampada, e define o valor correspondente (entre as constantes FORTE e FRACO) para o comparador do Timer1
  OCR1A=map(k,0,1024,FORTE,FRACO);  // 16.11.5 na datasheet 
}

void zeroCross() { // chamada automaticamente sempre que o pino 2 mudar para HIGH  
  TCCR1B=0x04; // 16.11.2 - habilita o timer no modo que divide o clock real por 256
  TCNT1=0;     // 16.11.4 - zera o contador do timer
  
  // a partir daqui, esta interrupcao encerra (ate a proxima repeticao do ciclo AC),  mas o ATmega vai começar a incrementar o valor de TCNT1 automaticamente a cada 
  // ciclo do Timer1, provocando uma nova interrupcao (TIMER1_COMPA) quando o contador alcancar o valor definido em OCR1A. Assim, se OCR1A for um valor alto, vai demorar mais para a interrupcao acontecer, 
  // e vice-versa. Essa demora corresponde ao tempo em que a lampada fica apagada.
}

ISR(TIMER1_COMPA_vect) { // chamada automaticamente sempre que o TCNT1 alcanca OCR1A.
  // manda um valor HIGH para o pino DIM, acendendo a lampada
  digitalWrite(PINO_DIM,HIGH);  
  // inicia um novo contador para o Timer1, calculado para provocar um overflow (e 
  // uma nova interrupcao - TIMER1_OVF) apos o numero de ciclos definido em PULSO.
  TCNT1 = 65536-PULSO;
  
  // a partir daqui, esta interrupcao encerra, e o ATmega comeca a incrementar o 
  // valor de TCNT1 automaticamente a cada ciclo do Timer1, provocando uma nova 
  // interrupcao (OVF) quando o contador entrar em overflow. Como o contador TCNT1
  // tem 16 bits, o overflow acontece quando ele ultrapassa o valor de 65535.
}

ISR(TIMER1_OVF_vect) { // chamada automaticamente sempre que ha overflow de TCNT1.
  // manda um valor LOW para o pino DIM, voltando a apagar a lampada
  digitalWrite(PINO_DIM,LOW);
  // desativa o timer que foi inicialmente ativado pela nossa funcao zeroCross().
  TCCR1B = 0x00;    
  
  // a partir daqui, esta interrupcao encerra. O normal eh que a proxima 
  // interrupcao a acontecer seja a do pino 2 (zeroCross()), no proximo 
  // meio ciclo AC.      
}

void loop() 
{
  
  int sinal;
  sinal=int((analogRead(pinopotenc)/34.1)+25); //Leitura do potenciometro com a escala convertida  
  
  while(ligado!=true){ //Loop para manter na tela de seleção de temperatura até o botão ser acionado e começar o funcionamento da estufa
    sinal=(analogRead(pinopotenc)/33.08)+25;
    lcd.setCursor(0, 0); //Coloca o cursor do display na coluna 0 e linha 0
    lcd.print("Inserir a Temp:"); // Exibe a mensagem com as variáveis
    lcd.setCursor(6, 1); //Coloca o cursor do display na coluna 6 e linha 1
    lcd.print(String(sinal)+"C"); // Exibe a mensagem com as variáveis
    
    if (digitalRead(buttonPin)==1){ //se o botão é acionado, limpa o lcd e inicia o aquecimento/leituras
      delay(1000);
      ligado=true;
      lcd.clear();
    }  
  }
  float h = dht.readHumidity(); //ler umidade
  float t = dht.readTemperature(); //ler temperatura

    lcd.setCursor(0, 0); //Coloca o cursor do display na coluna 0 e linha 0
    lcd.print("Temp: " + String(t) + "C"); // Exibe a mensagem com as variáveis
    lcd.setCursor(0, 1); 
    lcd.print("Um:");  
    lcd.print(int(h));
    lcd.setCursor(5,1);
    lcd.print(" | ");
    lcd.setCursor(8,1);
    lcd.print("Meta:"+String(sinal)+"C");

    attachInterrupt(0,zeroCross, RISING);

    
    if(int(sinal)-int(t)>=2){ // se a variação (meta-real) for maior que 4°C, ativa a potência máxima
      defineIntensidade(700);
    }
    if(int(sinal)-int(t)<=-3){ // se a variação (real-meta) for maior que 2°C, desativa a lampâda
      defineIntensidade(0);
    }
    if(int(sinal)-int(t)<2 && int(sinal)-int(t)>-3 ){
      Input= int(dht.readTemperature()); //Leitura temperatura
      Setpoint=int(sinal); //Leitura do potenciometro com a escala convertida 
      myPID.Compute();
      if(Output>30){
      Output=Output+200; //Converte o Output em uma escala aceitavél para a variação da potência da lampada
      }
      else{
        Output=Output+100;
        }
        defineIntensidade(Output); //ativa o Output enviado pelo PID no intervalo : limite inferior(4°C abaixo) / limite superior(2°C acima)
        }
    

    if(millis()-lastSend >100){
    lastSend=millis();

    Serial.print(Setpoint);
    Serial.print("  -  ");
    Serial.print(Input);
    Serial.print("  -  ");
    Serial.print(Output);
    Serial.println("");
  }
  
  if (digitalRead(buttonPin)==1){ // verifica se o botão foi pressionado para desligar o sistema de aquecimento e voltar no lcd inicial
    ligado=false;
    detachInterrupt(0);
    defineIntensidade(0);
    lcd.clear();
    delay(1000);
    }  
}
